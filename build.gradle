buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.2.0-A1"
group = "cpw.mods.forge"
archivesBaseName = "cursepacklocator"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.12.2-14.23.5.2768"
    runDir = "run"
    mappings = "stable_39"
}

ext.sharedManifest = manifest {
    attributes(
            ["Specification-Title"     : "cursepackloader",
             "Specification-Vendor"    : "cpw",
             "Specification-Version"   : "1", // Currently version 3 of the forgespi specification
             "Implementation-Title"    : project.name,
             "Implementation-Version"  : "${version}+${System.getenv("BUILD_NUMBER") ?: 0}",
             "Implementation-Vendor"   : "cpw",
             "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],
            "cpw/mods/forge/cursepacklocator/")
}

jar {
    manifest = project.manifest {
        from sharedManifest
    }
}

task zip(type: Zip, dependsOn: jar) {
    from (jar.outputs) { into ("libraries/cpw/mods/forge/cursepacklocator/${project.version}/") }
    from (file("1.12.2-cursepacklocator.json")) {
        filter { line -> line.replace('@version@', "${project.version}") }
        into ("versions/1.12.2-cursepacklocator/")
    }
}
